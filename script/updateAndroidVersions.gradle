// Snippet to include at the end of android/app/build.gradle
import groovy.json.JsonSlurper
import groovy.json.JsonOutput

tasks.register('updateAndroidVersions') {
    doLast {
        final jsonFile = new File('../../config/version.json')
        final resultJsonMap = new JsonSlurper().parseText(jsonFile.text)

        final rawPlatformVersions = [
            flutter.targetSdkVersion,
            flutter.compileSdkVersion
        ]
        
        final platformVersions = rawPlatformVersions.unique()

        final newJsonMap = [
                android :[
                    platforms: platformVersions.collect {
                        [version: it]
                    },
                    gradle: [version: gradle.gradleVersion]
            ]
        ]

        resultJsonMap.android.putAll(newJsonMap.android)

        println resultJsonMap

        final jsonStr = JsonOutput.toJson(resultJsonMap)
        final prettyStr = JsonOutput.prettyPrint(jsonStr)

        jsonFile.write("${prettyStr}\n")
    }
}
